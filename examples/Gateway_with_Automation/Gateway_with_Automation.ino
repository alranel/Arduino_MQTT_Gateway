/*
  This example shows how to configure a bunch of MQTT devices and have
  their properties synced with cloud variables.

  For this to work, you'll need to create the following variables in
  Arduino Cloud:
  * shelly_button     (type: string)
  * shellyplug_relay  (type: boolean)

  License: public domain.
*/

#include "arduino_secrets.h"
#include "thingProperties.h"
#include "Arduino_MQTT_Gateway.h"

void setup() {
  /* This is the standard boilerplate generated by Arduino Cloud */
  Serial.begin(9600);
  delay(1500); 
  initProperties();
  ArduinoCloud.begin(ArduinoIoTPreferredConnection);
  setDebugMessageLevel(2);
  ArduinoCloud.printDebugInfo();

  /*
    Here we configure a Shelly Button and a Shelly Plug S relay so 
    that they are always synced with two cloud variables.
    See below in the loop() how we can create automations.
  */

  // Shelly Button
  ArduinoMQTTGateway.add(shelly_button)
    .setStateTopic("shellies/shellybutton1-98CDAC2DACFD/input_event/0")
    .setStatePayloadJSONField("event");

  // Shelly Plug S - relay
  ArduinoMQTTGateway.add(shellyplug_relay)
    .setStateTopic("shellies/shellyplug-s-4022D889B588/relay/0")
    .setCommandTopic("shellies/shellyplug-s-4022D889B588/relay/0/command")
    .setStatePayload("on", "off")
    .setCommandPayload("on", "off");
}

void loop() {
  ArduinoCloud.update();
  ArduinoMQTTGateway.loop();

  /* Automations can be written in the loop() and will be automatically
     reflected on both Arduino Cloud and the MQTT device. */

  // Toggle the relay whenever the button is pressed once (the Shelly 
  // button emits a "S" string payload when pressed once, "SS" when 
  // pressed twice and so on).
  if (shelly_button == "S") {
    shellyplug_relay = !shellyplug_relay;
    shelly_button = "";  // Make sure you reset the button state variable
  }

  /*
  
    More ideas: if you're getting power measurements from your smart plug,
    you can also configure an overcurrent protection with the following 
    lines.

    if (shellyplug_power > 2000) {
      shellyplug_relay = false;
    }

  */
}

